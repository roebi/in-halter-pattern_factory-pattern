// Gradle < 2.1
// import org.reflections.Reflections

buildscript {
  apply from: "buildscript.gradle"
}

/*
buildscript { 
  dependencies {
    classpath "org.reflections:reflections:${reflectionsVersion}"
    classpath files('build/libs/in.halter.factory-pattern-1.0.jar')
  }
}
*/

// Gradle >= 2.1
plugins {
    id "java"
    id "idea"
    id "eclipse"
    // Helper for deploy to bintray
    id "com.jfrog.bintray" version "1.1"
    // Cobertura test coverage - A code coverage utility for Java.
    id "net.saliman.cobertura" version "2.2.8"
}

apply from: "publishing.gradle"
apply from: "cobertura.gradle"
apply from: "jacoco.gradle"
apply from: "sonarrunner.gradle"

def javaVersion = JavaVersion.VERSION_1_8
sourceCompatibility = javaVersion
targetCompatibility = javaVersion // defaults to sourceCompatibility

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  // runtime
  compile "org.apache.commons:commons-lang3:${apacheCommonsLang3Version}"
  compile "org.reflections:reflections:${reflectionsVersion}"
  compile "org.apache.logging.log4j:log4j-core:${log4jVersion}"

  // some for test
  testCompile "org.easytesting:fest-assert-core:${festAssertCoreVersion}"
  testCompile("org.easytesting:fest-assert:${festAssertVersion}") {
    exclude group: "org.easytesting", module: "fest-util"
  }
  testCompile "org.hamcrest:hamcrest-core:${hamcrestCoreVersion}"
  testCompile("junit:junit:${junitVersion}") {
    //excluding particular transitive dependency
    exclude group: "org.hamcrest", module: "hamcrest-core"
  }
  testCompile("org.hamcrest:hamcrest-library:${hamcrestLibraryVersion}") {
    //excluding particular transitive dependency
    exclude group: "org.hamcrest", module: "hamcrest-core"
  }
  testCompile("org.spockframework:spock-core:${spockCoreGroovyVersion}") {
    //excluding particular transitive dependency
    exclude group: "org.hamcrest", module: "hamcrest-core"
    exclude group: "junit", module: "junit"
    exclude group: "org.codehaus.groovy", module: "groovy-all"
  }
  // cobertura
  cobertura "org.slf4j:slf4j-simple:${coberturaSlf4jSimpleVersion}"
}

group = theGroup
version = theVersion
// mainClassName = group + ".app.Main"

compileJava {
  options.fork = true
}

ext.sharedManifest = manifest {
/*
  // Try to generate a Interaction List - chicken egg Problem - does not work here
  // Its in wrong Place, because i need the Command Class. It is in the Jar that i build.
  Reflections reflections = new Reflections("in.halter.pattern.factorypattern.impl.command")

  String commandClassName = "in.halter.pattern.factorypattern.api.Command"
  Class commandClass = null
  try {
    commandClass = reflections.getClass().getClassLoader().loadClass(commandClassName);
  } catch (ClassNotFoundException e) {
    // TODO Auto-generated catch block
    e.printStackTrace();
  }

  // Set<Class<? extends Command>>
  Set commandClasses = reflections.getSubTypesOf(commandClass)
  Map<String, String> commandClassesNames = new HashMap<String, String>()
  logger.quiet("All commandClasses")
  commandClasses.each { aCommandClass ->
    String fullClassName = aCommandClass.getName()
    int lastPointindex = fullClassName.lastIndexOf(".")
    String className = fullClassName.substring(lastPointindex  + 1)
    commandClassesNames.put(className, fullClassName)
    logger.quiet("- aCommandClass: " + className + ": " + fullClassName)
  }
*/
  attributes("Implementation-Title": group + " " + rootProject.name,
             "Implementation-Version": version,
             // "Class-Path": configurations.compile.collect { it.getName() }.join(' '))
             "Built-By": System.getProperty('user.name'),
             "Built-Date": new Date(),
             "Built-JDK": System.getProperty('java.version') )
}

jar {
  manifest = project.manifest {
    from sharedManifest
  }
}

task apiJar(type: Jar, dependsOn: jar) {
  appendix = "api"
  from files(sourceSets.main.output.classesDir) 
  exclude('in/halter/pattern/factorypattern/impl/**') 
  manifest = project.manifest {
    from sharedManifest
  }
}

task sourceJar(type: Jar, dependsOn: jar) {
  from sourceSets.main.allJava
  manifest = project.manifest {
    from sharedManifest
  }
}

task apisourceJar(type: Jar, dependsOn: jar) {
  appendix = "api"
  classifier = "sources"
  from sourceSets.main.allJava
  exclude('in/halter/pattern/factorypattern/impl/**') 
  manifest = project.manifest {
    from sharedManifest
  }
}

javadoc {
  title = "Factory Pattern for Commands and its CommandContext and CommandFactory for java programming language."
  options {
    links("http://docs.oracle.com/javase/8/docs/api/")
    // links("http://www.halter.in/factorypattern/docs/current/javadoc/")
  }
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
  classifier = "doc"
  from javadoc.destinationDir
  manifest = project.manifest {
    from sharedManifest
  }
}

task apijavaDocJar(type: Jar, dependsOn: javadoc) {
  appendix = "api"
  classifier = "doc"
  from javadoc.destinationDir
  exclude('in/halter/pattern/factorypattern/impl/**') 
  manifest = project.manifest {
    from sharedManifest
  }
}

test {
  useJUnit {
    // includeCategories 'org.gradle.junit.CategoryA'
  }
  testLogging {
    events "passed", "skipped", "failed"
  }
}

artifacts {
  archives jar, sourceJar, javaDocJar, apiJar, apisourceJar, apijavaDocJar
}
